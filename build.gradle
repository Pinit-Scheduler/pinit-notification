plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.0.21'
}

group = 'me'
version = '0.0.1-SNAPSHOT'
description = 'pinit-notification'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

kotlin {
    jvmToolchain(21)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.grpc:grpc-netty-shaded:1.68.2'
    implementation 'io.grpc:grpc-protobuf:1.68.2'
    implementation 'io.grpc:grpc-stub:1.68.2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

    // Actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Firebase Admin SDK
    implementation("com.google.firebase:firebase-admin:9.7.0") {
        exclude group: "commons-logging", module: "commons-logging"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.5"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.68.2"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    dependsOn(tasks.named('generateProto'))
}
tasks.named('compileKotlin') {
    dependsOn(tasks.named('generateProto'))
}
tasks.named('compileTestJava') {
    dependsOn(tasks.named('generateTestProto'))
}
tasks.named('compileTestKotlin') {
    dependsOn(tasks.named('generateTestProto'))
}
